plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.devtools.ksp' version '1.6.10-1.0.4'
}

android {
    compileSdkVersion 32
    namespace 'com.rerere.iwara4a'

    defaultConfig {
        applicationId "com.rerere.iwara4a"
        minSdkVersion 23
        targetSdkVersion 32

        // Version Meta
        versionCode 95
        versionName "1.38.6"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }
    signingConfigs {
        release {
            keyAlias 'iwara'
            keyPassword 'iwara4a'
            storeFile file('../.github/sign/iwara4a.jks')
            storePassword 'iwara4a'
        }
    }
    buildTypes {
        release {
            ndk {
                abiFilters 'arm64-v8a'
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

// ksp
kotlin {
    sourceSets {
        debug {
            kotlin.srcDir("build/generated/ksp/debug/kotlin")
        }
        release {
            kotlin.srcDir("build/generated/ksp/release/kotlin")
        }
    }
}

// 禁用掉烦人的警告
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += "-opt-in=androidx.compose.material3.ExperimentalMaterial3Api"
        freeCompilerArgs += "-opt-in=androidx.compose.material.ExperimentalMaterialApi"
        freeCompilerArgs += "-opt-in=androidx.compose.animation.ExperimentalAnimationApi"
        freeCompilerArgs += "-opt-in=androidx.compose.foundation.ExperimentalFoundationApi"
        freeCompilerArgs += "-opt-in=com.google.accompanist.pager.ExperimentalPagerApi"
        freeCompilerArgs += "-opt-in=coil.annotation.ExperimentalCoilApi"
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation 'androidx.activity:activity-compose:1.4.0'

    // Compose Lib
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.ui:ui-util:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"

    // material design
    implementation "androidx.compose.material3:material3:$md3_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    //implementation "androidx.compose.material3:material3-window-size-class:$md3_version"

    // Md3 Compat
    implementation 'com.github.re-ovo:md3compat:1.0.7'

    // Compose Setting
    implementation 'com.github.re-ovo:compose-setting:1.0.8'

    // xCrash
    implementation 'com.iqiyi.xcrash:xcrash-android-lib:3.1.0'

    // xLog
    implementation 'com.elvishew:xlog:1.11.0'
    
    // downloader
    implementation 'me.laoyuyu.aria:core:3.8.16'
    kapt 'me.laoyuyu.aria:compiler:3.8.16'

    // zoomable image
    implementation 'com.github.re-ovo:ZoomableComposeImage:1.0.4'

    // LeakCanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'

    // ExoPlayer
    implementation "com.google.android.exoplayer:exoplayer-core:$exoplayer_version"
    implementation "com.google.android.exoplayer:exoplayer-ui:$exoplayer_version"
    implementation "com.google.android.exoplayer:extension-okhttp:$exoplayer_version"

    // Splash screen
    implementation("androidx.core:core-splashscreen:1.0.0-beta02")

    // WindowManager
    implementation "androidx.window:window-java:1.0.0"

    // Slanted Text
    implementation 'com.github.re-ovo:slanted-text-compose:1.0.2'

    // Motion Animation
    def motion_anim_version = "0.9.0-alpha04"
    implementation "com.github.fornewid.material-motion-compose:core:$motion_anim_version"
    implementation "com.github.fornewid.material-motion-compose:navigation:$motion_anim_version"

    // Lottie
    implementation "com.airbnb.android:lottie-compose:$lottie_version"

    // Paging3
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    implementation "androidx.paging:paging-compose:1.0.0-alpha14"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    // Navigation for JetpackCompose
    implementation "androidx.navigation:navigation-compose:2.5.0-beta01"

    // Coil
    implementation "io.coil-kt:coil-compose:2.0.0-rc03"

    // accompanist
    implementation "com.google.accompanist:accompanist-pager:$acc_version"
    implementation "com.google.accompanist:accompanist-pager-indicators:$acc_version"
    implementation "com.google.accompanist:accompanist-swiperefresh:$acc_version"
    implementation "com.google.accompanist:accompanist-flowlayout:$acc_version"
    implementation "com.google.accompanist:accompanist-placeholder-material:$acc_version"
    implementation "com.google.accompanist:accompanist-navigation-animation:$acc_version"
    implementation "com.google.accompanist:accompanist-webview:$acc_version"

    // Okhttp
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation 'com.squareup.okhttp3:okhttp-dnsoverhttps:4.9.3'

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // JSOUP
    implementation 'org.jsoup:jsoup:1.14.3'

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // Test
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
}